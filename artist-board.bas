10 REM ********************
11 REM ***              ***
12 REM *** ARTIST BOARD ***
13 REM ***              ***
14 REM ********************
15 REM
16 REM
20 GOSUB 1000: REM INSTRUCTIONS
30 GOSUB 2000: REM SETUP
40 GOSUB 3000: REM PLAY
50 GOSUB 4000: REM END
60 END
1000 :
1001 REM *** INSTRUCTIONS
1002 :
1005 POKE 53281,0: POKE 53280,12: 
1010 PRINT "{clear}{gray}"TAB(10)"*** ARTIST BOARD ***"
1025 PRINT: PRINT
1030 PRINT "BY USING ARTIST BOARD AND YOUR CREATIVE"
1031 PRINT "TALENT, YOU CAN CREATE MOSAIC-LIKE"
1032 PRINT "PICTURES.": PRINT
1040 PRINT: PRINT "THE JOYSTICK IS USED TO MOVE THE CROSS"
1041 PRINT "HAIR (YOUR PAINT BRUSH)."
1042 PRINT: PRINT "PLUG THE JOYSTICK INTO GAME PORT #1"
1043 PRINT
1045 PRINT "THE TOOLS ARE THERE FOR YOU TO CREATE"
1046 PRINT "SOME COLORFUL MASTERPIECES... GO TO IT!"
1050 PRINT: PRINT "yOU MAY SAVE A DRAWING ONTO DISK FOR"
1051 PRINT "RECALL AT A LATER TIME.": PRINT: PRINT: PRINT
1060 PRINT "PRESS RETURN TO BEING PAINTING!"
1065 GET ANS$: IF ANS$<>CHR$(13) THEN GOTO 1065
1990 RETURN
2000 :
2001 REM *** SETUP
2002 :
2005 SID=54272: FOR I=SID TO SID+24: POKE I,0: NEXT: POKE SID+5,9
2020 PLT=49152: FOR I=0 TO 24: READ C: POKE PLT+I,C: NEXT I
2021 DATA 32,16,192,132
2022 DATA 253,32,16,192
2023 DATA 152,170,164,253
2024 DATA 24,76,240,255
2025 DATA 32,253,174,32
2026 DATA 158,173,76,170,177
2030 CO$="{black}{white}{red}{cyan}{purple}{green}{blue}{yellow}{orange}{brown}{pink}{dark gray}{gray}{light green}{light blue}{light gray}"
2130 PDL=49664: FOR I=0 TO 63: READ C: POKE PDL+I,C: NEXT I
2131 DATA 162,1,120,173,2,220,141,0,193,169,192,141,2,220,169
2132 DATA 128,141,0,220,160,128,234,136,16,252,173,25,212,157
2133 DATA 1,193,173,26,212,157,3,193,173,0,220,9,128,141,5,193
2134 DATA 169,64,202,16,222,173,0,193,141,2,220,173,1,220,141
2135 DATA 6,193,88,96
2990 RETURN
3000 :
3001 REM PLAY
3002 :
3003 PRINT "{orange}{clear}";: X=20: Y=10
3004 FOR I=1 TO 21: PRINT "{reverse on}{black}                                        ";: NEXT
3005 SYS PLT,0,21
3006 PRINT "{reverse on}{white}  {black}  {white}  {red}  {cyan}  {purple}  {green}  {blue}  {yellow}  {orange}  {brown}  {pink}  {dark gray}  {gray}  {light green}  {light blue}  {light gray}  {white}  ";
3007 PRINT "{reverse on}{white}      A B C D E F G H I J K L M N O P   "
3008 PRINT "{reverse off}      TYPE LETTER TO CHANGE COLOR       "
3009 PRINT "  R=RECALL  S=SAVE  Q=QUIT  Z=ERASE(BLK)";: POKE 56215,1: POKE 1943,160
3010 JV=PEEK(56321) AND 15: JV=15-JV
3011 JX=JV AND 12: JY=JV AND 3
3013 IF JX=8 THEN X=X+1: IF X>39 THEN X=39
3014 IF JX=4 THEN X=X-1: IF X<0 THEN X=0
3015 IF JY=2 THEN Y=Y+1: IF Y>20 THEN Y=20
3016 IF JY=1 THEN Y=Y-1: IF Y<0 THEN Y=0
3017 SYS PLT,X,Y: PRINT "{reverse on} ";: C=PEEK(55296+X+40*Y) AND 15: SYS PLT,X,Y: PRINT "{reverse off}{white}+";
3018 FOR I=1 TO 30: NEXT I: SYS PLT,X,Y: PRINT "{reverse on}"MID$(CO$,C+1,1)" ";
3020 GET KEY$: IF KEY$="" THEN GOTO 3010
3040 IF KEY$>="A" AND KEY$<="P" THEN PRINT MID$(CO$,ASC(KEY$)-ASC("A")+1,1);: GOTO 3010
3050 IF KEY$="S" THEN GOTO 3100
3060 IF KEY$="R" THEN GOTO 3200
3070 IF KEY$="Q" THEN RETURN
3080 IF KEY$="Z" THEN GOTO 3003
3090 GOTO 3010
3100 SYS PLT,0,23
3101 PRINT "{reverse off}                                        "
3102 PRINT "{reverse off}                                        ";
3103 FI$=""
3105 SYS PLT,0,23: INPUT"{white} SAVE AS WHAT FILE";FI$: IF FI$="" THEN GOTO 3005
3110 OPEN 2,8,2,"@:"+FI$+",S,W"
3115 FOR I=0 TO 839: PRINT#2,CHR$(PEEK(55296+I)AND 15);: NEXT I
3120 CLOSE 2: GOTO 3005r
3200 SYS PLT,0,23
3201 PRINT "{reverse off}                                        "
3202 PRINT "{reverse off}                                        ";
3205 SYS PLT,0,23: INPUT "{white} RECALL WHAT FILE";FI$: IF FI$="" THEN GOTO 3005
3210 OPEN 2,8,2,FI$+",S,R"
3215 FOR I=0 TO 839: GET#2,A$: POKE 55296+I,ASC(A$+CHR$(0)): NEXT I
3220 CLOSE 2: GOTO 3005
3990 RETURN
4000 :
4001 REM END
4002 :
4010 PRINT "{clear}{white}"
4990 RETURN