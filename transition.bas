10 REM ********************
11 REM ***              ***
12 REM ***  TRANSITION  ***
13 REM ***              ***
14 REM ********************
15 REM
16 REM
20 GOSUB 1000
30 GOSUB 2000
40 GOSUB 3000
50 GOSUB 4000
90 END
1000 :
1010 REM *** INSTRUCTIONS
1020 :
1030 POKE 53280,6: POKE 53281,6
1040 VT$="{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}"
1050 GOSUB 1900
1060 PRINT "THE GAME OF TRANSITION WILL PRESENT YOU"
1070 PRINT "WITH A LIST OF NINE DIGITS. tHE LIST"
1080 PRINT "WILL LOOK LIKE THIS:"
1090 PRINT LEFT$(VT$,13);
1100 PRINT TAB(5);"[ 1 2 3 4 5 6 7 8 9 ]":PRINT
1105 PRINT TAB(5);"[ # # # # . ? ? ? ? ]"
1110 GOSUB 1800: GOSUB 1900
1120 PRINT "THE '#' CHARACTER CAN ONLY MOVE TO THE"
1130 PRINT "RIGHT, AND THE '?' CHARACTER CAN ONLY"
1140 PRINT "MOVE TO THE LEFT.": PRINT
1150 PRINT "A MOVE IS MADE BY MOVING TO AN EMPTY"
1160 PRINT "SPACE, OR BY JUMPING OVER ONE OPPOSING"
1170 PRINT "PIECE.": PRINT
1180 PRINT "TO MAKE A MOVE, YOU ENTER THE POSITION"
1190 PRINT "NUMBER OF THE MOVING PIECE. TO QUIT,"
1200 PRINT "ENTER ZERO (O)."
1800 PRINT LEFT$(VT$,24);"PRESS {reverse on}RETURN{reverse off} WHEN READY TO CONTINUE:{reverse on}"
1810 GET AN$: IF AN$="" THEN GOTO 1810
1890 RETURN
1900 PRINT "{clear}{white}{down}{down}";TAB(11);"*** TRANSITION ***": PRINT: PRINT
1990 RETURN
2000 :
2010 REM *** SETUP ***
2020 :
2030 DIM LI(9)
2040 FOR I=1 TO 4: LI(I)=1: LI(10-I)=2
2050 NEXT: NM=0
2990 RETURN
3000 :
3010 REM *** PLAY ***
3020 :
3030 GOSUB 1900
3040 PRINT LEFT$(VT$,8);TAB(5);"[";: FOR I=1 TO 9: PRINT I;: NEXT: PRINT "]": PRINT
3050 PRINT TAB(5);"[";: FOR I=1 TO 9: PRINT MID$(" .  #  ? ",LI(I)*3+1,3);: NEXT
3060 PRINT "]": PRINT: PRINT
3070 IF LI(5) THEN GOTO 3090
3080 WL=0: FOR I=1 TO 4: WL=WL+(LI(I)=2)+(LI(I+5)=1): NEXT: IF WL<=-8 THEN GOTO 3990
3090 PRINT TAB(11);"#--->     <---?": PRINT
3100 PRINT TAB(13);"MOVE (0-9): {reverse on} {reverse off}";
3110 GET AN$: IF AN$<"0" OR AN$>"9" THEN GOTO 3110
3120 IF AN$="0" THEN WL=0: GOTO 3990
3130 AN=VAL(AN$): PRINT "{left}";AN$
3140 IF LI(AN)=0 THEN ER$="THAT SPACE IS EMPTY.": GOTO 3220
3150 IF LI(AN)=1 THEN DI=1
3160 IF LI(AN)=2 THEN DI=-1
3170 IF AN+DI>9 OR AN+DI<1 THEN GOTO 3210
3180 IF LI(AN+DI)=0 THEN LI(AN+DI)=LI(AN): GOTO 3250
3190 IF AN+DI+DI<1 OR AN+DI+DI>9 THEN GOTO 3210
3200 IF LI(AN+DI)<>LI(AN) AND LI(AN+DI+DI)=0 THEN LI(AN+DI+DI)=LI(AN): GOTO 3250
3210 ER$="IT CANNOT MOVE FURTHER."
3220 PRINT LEFT$(VT$,18);ER$
3230 FOR DELAY=1 TO 800: NEXT
3240 PRINT LEFT$(VT$,18);: FOR J=1 TO LEN(ER$): PRINT " ";: NEXT: GOTO 3040
3250 LI(AN)=0: NM=NM+1: GOTO 3040
3990 RETURN
4000 :
4010 REM *** END ***
4020 :
4030 PRINT LEFT$(VT$,18);"THE GAME IS OVER!": PRINT
4040 IF WL THEN GOTO 4070
4050 PRINT "YOU GOT STUCK AFTER";NM;"MOVES.": PRINT "BETTER LUCK NEXT TIME!"
4060 GOTO 4990
4070 PRINT "YOU DID IT!": PRINT "AND IT ONLY TOOK YOU";NM;"MOVES."
4990 RETURN