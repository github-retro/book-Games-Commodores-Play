10 REM ********************
11 REM ***              ***
12 REM ***  AIR ATTACK  ***
13 REM ***              ***
14 REM ********************
15 REM
16 REM
20 GOSUB 1000: REM INSTRUCTIONS
30 GOSUB 2000: REM SETUP
40 GOSUB 3000: REM PLAY
50 GOSUB 4000: REM END
60 END
1000 :
1001 REM *** INSTRUCTIONS
1002 :
1030 POKE 53280,14: POKE 53281,14: PRINT "{clear}"
1040 PRINT TAB(11)"{black}*** AIR ATTACK ***"
1050 PRINT: PRINT: PRINT
1060 PRINT "IN THIS GAME YOU ARE A RIGHTER PILOT. YOU SCORE BY HITTING ONE ";
1065 PRINT "OF THE ENEMY SHIPS WITH ONE OF YOUR BOMBS AND SINKING IT."
1070 PRINT
1080 PRINT "TO DROP A BOMB, SIMPLY PRESS ANY KEY ON THE KEYBOARD. YOUR SCORE ";
1085 PRINT "FOR HITTING A SIHP WILL DEPEND ON WHICH PART OF THE SHIP YOU HIT."
1090 PRINT
1100 PRINT "IF YOU HIT THE LOWER DECK, YOU SCORE 10 POINTS. IF YOU HIT THE ";
1105 PRINT "UPPER DECK, YOU SCORE 20 POINTS. IF YOU HIT THE SMOKE-STACK ";
1106 PRINT "YOU HAVE DONE VERY WELL, AND ARE REWARDED WITH 30 POINTS."
1110 PRINT: PRINT
1120 PRINT "PRESS *RETURN* TO CONTINUE";
1125 GET X$: IF X$<>CHR$(13) THEN GOTO 1125
1130 PRINT "{clear}"
1135 PRINT TAB(11)"{black}*** AIR ATTACK ***"
1140 PRINT: PRINT: PRINT
1145 PRINT "YOU HAVE AN ARSENAL OF 15 BOMBS. THE SPEED OF EACH SHIP WILL ";
1150 PRINT "VARY, SO MAKE EVERY SHOT COUNT!"
1155 PRINT: PRINT: PRINT "GOOD LUCK!"
1160 PRINT: PRINT: PRINT "PRESS *RETURN* TO CONTINUE";
1170 GET X$: IF X$<>CHR$(13) THEN GOTO 1170
1990 RETURN
2000 :
2001 REM *** SETUP
2002 :
2020 PLT=49152: FOR I=0 TO 24: READ C: POKE PLT+I,C: NEXT I
2021 DATA 32,16,192,132
2022 DATA 253,32,16,192
2023 DATA 152,170,164,253
2024 DATA 24,76,240,255
2025 DATA 32,253,174,32
2026 DATA 158,173,76,170,177
2030 SL=15
2050 PRINT "{clear}": POKE 53281,14: POKE 53280,14
2060 SYS PLT,0,22: PRINT "{reverse on}{blue}                                        ";
2065 SYS PLT,0,23: PRINT "{reverse on}{blue}                                        ";
2090 AX=0: SX=33: SS=1
2100 DEF FN CO(X)=PEEK(55296+FX+40+FY) AND 15
2990 RETURN
3000 :
3001 REM *** PLAY
3002 :
3040 SYS PLT,0,24: PRINT "{white}{reverse off}SHOTS LEFT:"SL"{left} "TAB(18)"SCORE:"TS"{left} ";
3050 GOSUB 3090: REM PLANE
3060 GOSUB 3130: IF SL=0 THEN RETURN
3070 GOSUB 3270: REM SHIP
3080 GOTO 3050
3090 :
3100 REM LINE 3110 CHECKS TO SEE IF THE PLANE IS AT -1. IF SO, THE PLAINE IS OFF SCREEN. AND REVERTS TO 33, THE RIGHT HAND
3101 REM SIDE OF THE PICTURE. FROM THERE THE CYCLE BEGINS AGAIN
3110 AX=AX-1: IF AX<0 THEN AX=33: SYS PLT,0,0: PRINT "{light blue}       {down}{left}{left}{left}{left}{left}{left}{left}      ";
3115 SYS PLT,AX,0
3120 PRINT "{reverse off}{light blue}   {white}... {black}{reverse on}{sh pound}{reverse off} {down}{left}{left}{left}{left}{left}{left}{left}{reverse on}{cm asterisk} - -{reverse off} ";
3125 RETURN
3130 IF FF<>0 THEN GOTO 3160
3140 GET X$: IF X$="" THEN RETURN
3150 FF=1: FX=AX+3: FY=2: BC=1
3160 BC=BC+1: IF BC<2 THEN RETURN
3165 SYS PLT,FX,FY: PRINT "{reverse off}{light blue} ";
3166 BC=0
3170 FY=FY+1
3175 CO=FN CO(O)
3180 IF CO<>15 AND CO<>6 THEN SYS PLT,FX,FY: PRINT "{yellow}-";: RETURN
3190 IF CO=6 THEN GOTO 3230
3200 TS=TS+INT(22-FY+.5)*10:IF FX=SX+4 THEN TS=TS+10
3210 SC=0: SYS PLT,SX,20: PRINT "{reverse off}{light blue}       {down}{left}{left}{left}{left}{left}{left}{left}      ";
3220 SX=33: SS=1: SC=0
3230 SYS PLT,FX-2,20: PRINT"{white}- {reverse on} {reverse off} -{down}{left}{left}{left}{left}{left}{left}{left}- -";
3235 FOR PA=1 TO 50: NEXT PA
3240 SYS PLT,FX-2,20: PRINT "{reverse off}{light blue}       {down}{left}{left}{left}{left}      ";
3250 FF=0: SL=SL-1
3260 SYS PLT,0,24: PRINT"{white}{reverse off}SHOTS LEFT:"SL"{left} "TAB(18)"SCORE:"TS"{left} ";: RETURN
3270 SC=SC+1: IF SC<SS THEN RETURN
3280 SC=0: SYS PLT,SX,20: PRINT"{reverse off}{light blue}       {down}{left}{left}{left}{left}{left}{left}{left}      ";
3290 SX=SX+1: IF SX>33 THEN SX=0: SS=1+INT(3*RND(0))
3300 SYS PLT,SX,20: PRINT "{light gray}     {cm a}{reverse on} {reverse off}{cm s}{down}{left}{left}{left}{left}{left}{left}{sh pound}{reverse on}      {reverse off}{sh asterisk}";: RETURN
3990 RETURN
4000 :
4001 REM ** END
4002 :
4030 PRINT "{home}": PRINT "{black}THE GAME IS OVER": PRINT "YOUR SCORE OF"TS" IS ";
4040 IF TS<25 THEN PRINT "ROTTEN": RETURN
4050 IF TS<50 THEN PRINT "BAD!": RETURN
4060 IF TS<75 THEN PRINT "POOR!": RETURN
4070 IF TS<100 THEN PRINT"FAIR...": RETURN
4080 IF TS<150 THEN PRINT "GOOD...": RETURN
4090 IF TS<250 THEN PRINT "GREAT!": RETURN
4100 IF TS<450 THEN PRINT "FANTASTIC!": RETURN
4110 PRINT "PERFECT!": RETURN
4990 RETURN