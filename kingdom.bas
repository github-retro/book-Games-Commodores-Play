10 REM ********************
11 REM ***              ***
12 REM ***   KINGDOM    ***
13 REM ***              ***
14 REM ********************
15 REM
16 REM
20 GOSUB 1000
30 GOSUB 2000
40 GOSUB 3000
50 GOSUB 4000
60 END
1000 :
1001 REM *** INSTRUCTIONS
1002 :
1010 POKE 53280,9: POKE 53281,0: PRINT"{clear}{white}";
1015 VT$="{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}"
1020 PRINT LEFT$(VT$,3);TAB(12);"*** KINGDOM ***"
1030 PRINT LEFT$(VT$,6)
1040 PRINT "THIS IS A SIMULATION OF THE COUNTRY OF"
1050 PRINT "SUMERIA. YOU ARE THE SOVEREIGN RULER"
1060 PRINT "AND YOU WILL GOVERN FOR TEN YEARS."
1070 PRINT: PRINT "THE DECISIONS THAT YOU MAKE WILL AFFECT"
1080 PRINT "THE LIVES OF HUNDRES OF PEOPLE. YOUR"
1090 PRINT "DICTATORIAL SKILLS WILL BE RATED ONCE"
1100 PRINT "YOUR REIGN HAS ENDED."
1110 PRINT: PRINT "YOU WILL BE ASKED TO MAKE SEVERAL KEY"
1120 PRINT "DECISIONS EACH YEAR. EACH OF THESE"
1130 PRINT "WILL BE EXPLAINED TO YOU."
1140 PRINT LEFT$(VT$,23)
1150 PRINT "PRESS ANY KEY TO CONTINUE"
1160 GET ANS$: IF ANS$="" THEN GOTO 1160
1990 RETURN
2000 :
2001 REM *** SETUP ***
2002 :
2010 P=95: S=2800: H=3000: E=H-S: Y=3: A=H/Y: I=5: D=0: Z=0: Q=1
2020 DIM NU$(11): FOR J=1 TO 11: READ NU$(J): NEXT
2030 DATA FIRST,SECOND,THIRD,FOURTH,FIFTH,SIXTH
2040 DATA SEVENTH,EIGHTH,NINTH,TENTH,ELEVENTH
2990 RETURN
3000 :
3001 REM *** PLAY ***
3002 :
3005 PRINT "{clear}";LEFT$(VT$,3);TAB(12);"*** KINGDOM ***";LEFT$(VT$,6)
3010 Z=Z+1: PRINT: PRINT "HAMURABI, I BEG TO REPORT TO YOU:"
3020 PRINT "IN THE "NU$(Z)" YEAR,"D" PEPOLE STARVED."
3030 PRINT "A TOTAL OF "I" CAME TO THE CITY."
3100 IF Q=0 THEN P=INT(P/2): PRINT: PRINT "A HORRIBLE PLAUGE STRUCK! HALF ";
3110 IF Q=0 THEN PRINT "OF YOUR PEOPLE PERISHED."
3120 PRINT
3130 PRINT "THE POPULATION IS"P
3131 PRINT "THE CITY OWNS"A"ACRES.": PRINT "YOU HARVESTED"Y"BUSHELS PER ACRE."
3232 PRINT "RATS ATE A TOTAL OF"E"BUSHELS": PRINT"YOU HAVE "S"BUSHELS IN RESERVE."
3250 IF Z=11 THEN RETURN
3400 C=INT(RND(1)*10): Y=C+17
3410 PRINT: PRINT "LAND IS TRADING AT "Y"BUSHELS PER ACRE."
3411 INPUT "HOW MANY ACRES DO YOU WISH TO BUY";Q
3420 IF Q<0 THEN PRINT "JAMURABI, YOU CANNOT DO THAT. IF YOU WISH TO SELL ";
3421 IF Q<0 THEN PRINT "LAND, BUY 0 ACRES FIRST.": GOTO 3410
3425 IF Y*Q=S THEN PRINT "SIR, KEEP AT LEAST ONE BUSHEL TO PLANT.": GOTO 3410
3430 IF Y*Q>S THEN PRINT "HAMURABI, THINK AGAIN! YOU ONLY HAVE"
3431 IF Y*Q>S THEN PRINT S"BUSHELS OF GRAIN.": GOTO 3410
3450 IF Q>0 THEN A=A+Q: S=S-Y*Q: C=0: GOTO 3500
3460 PRINT: INPUT "HOW MANY ACRES WOULD YOU SELL";Q
3465 IF Q<0 THEN PRINT "HAMURABI, YOU CAN NOT DO THAT. IF YOU"
3466 IF Q<0 THEN PRINT "DON'T WANT TO SELL, ENTER 0 ACRES.": GOTO 3460
3468 IF A-Q=0 THEN PRINT "YOU MUCH KEEP AT LEAST ONE ACRE OF LAND.": GOTO 3460
3480 A=A-Q: S=S+Y*Q: C=0
3500 PRINT: PRINT "OF THE "S"BUSHELLS REMAINING, HOW MANY"
3501 INPUT "DO YOU WISH TO FEED YOUR PEOPLE";Q
3502 IF S-Q=0 THEN PRINT "SIR, KEEP AT LEAST ONE BUSHEL TO PLANT": GOTO 3500
3505 IF Q<1 THEN PRINT: PRINT "HAMURABI, THE PEOPLE WILL STRAVE!"
3506 IF Q<1 THEN PRINT "YOU MUST FEED THEM SOMETHING.": GOTO 3500
3520 S=S-Q: C=1
3525 PRINT: PRINT "YOU NOW HAVE "S"BUSHELS"
3530 PRINT: PRINT "OF THE "A"ACRES YOU OWN, HOW MANY"
3531 PRINT "DO YOU WISH TO PLANT WITH SEED?"
3532 INPUT "(PLANT 2 ACRES WITH ONE BUSHEL");D
3535 IF D<1 THEN PRINT: PRINT "HAMURABI, YOU MUST PLANT SOMETHING SO"
3536 IF D<1 THEN PRINT "THERE WILL BE FOOD FOR THE NEXT YEAR": GOTO 3530
3550 IF D>10*P THEN PRINT "SIR, ONE PERSON CAN ONLY WORK TEN ACRES"
3551 IF D>10*P THEN PRINT "YOUR POPULATION OF"P"IS NOT BIG ENOUGH.": GOTO 3530
3553 IF D/2>2 THEN PRINT "HAMURABI, THAT IS TOO MUCH TO PLANT": GOTO 3530
3600 Y=C: H=D*Y: E=0: C=INT(RND(1)*5)+1: IF INT(C/2)*2=C THEN E=INT(S/C)
3610 S=S-E+H: C=INT(RND(1)*5)+1: I=INT(C*(20*A+S)/P/100+1): C=INT(Q/20)
3611 Q=INT(10*(2*RND(1)-.3)): IF P<C THEN D=0: GOTO 3010
3615 D=P-C: IF D>.5*P THEN GOTO 3630
3620 P1=((Z-1)*P1+D*100/P)/Z: P=C: D1=D1+D: GOTO 3010
3630 PRINT: PRINT "YOU STARVED"D"PEOPLE IN ONE YEAR!"
3631 PRINT "YOU HAVE DONE SUCH A MISERABLE JOB THAT"
3632 PRINT "YOU HAVE BEEN OVERTHROWN AND DEPOSED!"
3633 WL=1: RETURN
4000 :
4001 REM *** END ***
4002 :
4005 IF WL THEN RETURN: REM ENDS PROGRAM IF RULER WAS DEPOSED
4010 PRINT: PRINT "IN YOUR TEN YEARS OF RULE,"P1"%"
4020 PRINT "OF THE POPULATION STARVED PER YEAR, ON THE AVERAGE."
4030 PRINT "A TOTAL OF "D1"PEOPLE DIED.": L=A/P
4040 PRINT
4050 PRINT "YOU STARTED WITH 10 ACRES PER PERSON"
4060 PRINT "AND ENDED WITH"L"ACRES"
4061 PRINT "PER PERSON."
4070 IF P1>33 OR L<7 THEN PRINT "YOU ARE A DISGRACE! THE PEOPLE HAVE"
4071 IF P1>33 OR L<7 THEN PRINT "EXILED YOU TO A REMOTE ISLAND.": RETURN
4080 IF P1>10 OR L<9 THEN PRINT "YOU RULE LIKE THE AYATOLLAH! MOST OF"
4081 IF P1>10 OR L<9 THEN PRINT "YOUR SUBJECTS WOULD DANCE ON YOUR GRAVE.": RETURN
4090 IF P1>3 OR L<10 THEN PRINT "YOU COULD HAVE DONE BETTER."INT(P*.8*RND(1))
4091 IF P1>3 OR L<10 THEN PRINT "PEOPLE WOULD LOVE TO ASSASSINATE YOU!": RETURN
4095 PRINT "A GREAT JOB! YOU CAN RULE MY COUNTRY"
4096 PRINT "ANYTIME YOU WANT!": RETURN

