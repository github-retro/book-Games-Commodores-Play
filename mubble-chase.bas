10 REM ********************
11 REM ***              ***
12 REM *** MUBBLE CHASE ***
13 REM ***              ***
14 REM ********************
15 REM
16 REM
20 GOSUB 1000: REM INSTRUCTIONS
30 GOSUB 2000: REM SETUP
40 GOSUB 3000: REM PLAY
50 GOSUB 4000: REM END
60 END
1000 :
1001 REM *** INSTRUCTIONS
1002 :
1010 POKE 53280,12: POKE 53281,12: PRINT "{clear}"
1020 PRINT TAB(10)"{black}*** MUBBLE CHASE ***"
1025 PRINT: PRINT
1030 PRINT "IN THIS EXCITING GAME, YOU CONTROL THE MOVEMENT OF THE HUNGRY ";
1031 PRINT "LITTLE CREATURE WE CALL THE MUBBLE. THE MUBBLE SCURRIEST THROUGHT ";
1032 PRINT "A MAZE, TRYING TO EAT UP ALL OF THE FLOWER POINTS."
1035 PRINT
1040 PRINT "UNFORUNATELY, THERE ARE THREE MUBBLE-EASTERS IN THE SAME MAZE, ";
1041 PRINT "WHO WANT TO CATCH AND EAT THE POOR MUBBLE."
1050 PRINT: PRINT: PRINT "PRESS *RETURN* WHEN READY TO CONTINUE";
1055 GET X$: IF X$<>CHR$(13) THEN GOTO 1055
1060 PRINT "{clear}"
1065 PRINT TAB(10)"{black}*** MUBBLE CHASE ***"
1066 PRINT: PRINT
1070 PRINT "YOU MUST MANEUVER THE MUBBLE TO THE"
1071 PRINT "FLOWER POINTS AND AWAY FROM THE MUBBLE"
1072 PRINT "EASTERS. YOU ARE ALLOWED TO LOSE TWO"
1073 PRINT "MUBBLES, BUT WHEN THE THIRD MUBBLE IS"
1074 PRINT "EATEN, THE GAME IS OVER."
1075 PRINT
1076 PRINT "ALSO THE MUBBLE GROWS AS HE CONSUMES THE FLOWERS, MAKING IT ";
1077 PRINT "HARDER TO AVOID THE MUBBLE EATERS!"
1080 PRINT
1085 PRINT "MOVEMENT OF THE MUBBLE IS CONTROLLED BY USING THE LETTERS A,Z,J,K."
1090 PRINT
1091 PRINT TAB(16)"UP"
1092 PRINT TAB(16)"A"
1093 PRINT TAB(7)"LEFT - J K - RIGHT"
1094 PRINT TAB(16)"Z"
1095 PRINT TAB(15)"DOWN"
1100 PRINT: PRINT "PRESS *RETURN* WHEN READY TO CONTINUE";
1105 GET X$: IF X$<>CHR$(13) THEN GOTO 1105
1990 RETURN
2000 :
2001 REM *** SETUP
2002 :
2003 SID=54272
2015 DIM ME(3,2),SP(3)
2020 PLT=49152: FOR I=0 TO 24: READ C: POKE PLT+I,C: NEXT I
2021 DATA 32,16,192,132
2022 DATA 253,32,16,192
2023 DATA 152,170,164,253
2024 DATA 24,76,240,255
2025 DATA 32,253,174,32
2026 DATA 158,173,76,170,177
2030 DIM CO$(115): FOR I=0 TO 15: READ CO$(I): NEXT
2031 DATA "{black}","{white}","{red}","{cyan}","{purple}","{green}","{blue}","{yellow}"
2032 DATA "{orange}","{brown}","{pink}","{dark gray}","{gray}","{light green}","{light blue}","{light gray}"
2035 PT=0: MU=3: NM=4: POKE 53281,0: POKE 53280,0
2041 PRINT "{clear}{reverse on}{white}                                       "
2043 FOR I=1 TO 19 STEP 3
2044 SYS PLT,0,I: PRINT "{reverse on}{white} {reverse off}                                     {reverse on} "
2045 PRINT "{reverse on}{white} {right}{light blue}   {right}   {right}   {right}   {right}   {right}   {right}   {right}   {right}   {right}{white} "
2046 PRINT "{reverse on}{white} {right}{light blue}   {right}   {right}   {right}   {right}   {right}   {right}   {right}   {right}   {right}{white} "
2047 NEXT
2048 PRINT "{reverse on}{white} {reverse off}                                     {reverse on}"
2049 PRINT "{reverse on}{white}                                       "
2050 PRINT "{green} * * *";
2060 DIM MU(8,2)
2070 FOR I=5 TO 33 STEP 4
2071 FOR J=4 TO 19 STEP 3
2072 C$=MID$("{pink}{light green}{yellow}{purple}{green}",1+5*RND(0),1): SYS PLT,I,J: PRINT "{reverse off}"C$"*";
2074 NEXT: NEXT
2080 DEF FN SC(V)=PEEK(1024+X2+40*Y2+V)
2085 DEF FN CO(V)=PEEK(55296+X2+40*Y2+V)AND 15
2990 RETURN
3000 :
3001 REM *** PLAY
3002 :
3005 ME(1,1)=1: ME(1,2)=1: ME(2,1)=37: ME(2,2)=1
3006 ME(3,1)=37: ME(3,2)=22: DE(1)=1: DE(2)=4: DE(3)=3
3007 FOR I=1 TO 3: SYSPLT,ME(I,1),ME(I,2): PRINT "{white}o";: SP$(I)=" ": NEXT
3008 FOR I=1 TO NM: MU(I,1)=I: MU(I,2)=22: NEXT
3009 MX=NM: MY=22
3010 FOR I=1 TO NM: SYSPLT,MU(I,1),MU(I,2): PRINT "{green}+";: NEXT: MD=1: HI=O: MM=0
3011 MU=MU-1: IF MU<0 THEN RETURN
3015 SYS PLT,15,24: PRINT "{white}<<< READY >>>";: FOR I=1 TO 2000: NEXT
3016 SYS PLT,15,24: PRINT "             ";
3020 GOSUB 3300: REM MOVE MUBBLE
3025 IF PT=48 THEN RETURN
3030 GOSUB 3400: REM MOVE MUBBLE EATERS
3040 IF HI=0 THEN GOTO 3020
3041 POKE SID+24,15: POKE SID+5,9: POKE SID+6,0
3042 FOR I=15 TO 0 STEP -1: POKE SID+1,10+40*RND(0): POKE SID+24,I
3043 POKE SID+4,33: FOR PA=1 TO 10: NEXT
3044 POKE SID+4,32: NEXT: POKE SID,0: POKE SID+1,0
3045 FOR I=1 TO 3: SYS PLT,ME(I,1),ME(I,2): PRINT SP$(I);: NEXT
3050 FOR I=1 TO NM: SYS PLT,MU(I,1),MU(I,2): PRINT " ";: NEXT
3055 GOTO 3005
3300 FL=((MX+3)/4)=INT((MX+3)/4) AND ((MY+2)/3)=INT((MY+2)/3)
3301 IF NOT FL THEN GOTO 3320
3304 GET K$: IF K$="" THEN GOTO 3320
3310 IF K$="A" THEN MD=2
3311 IF K$="K" THEN MD=1
3312 IF K$="Z" THEN MD=4
3313 IF K$="J" THEN MD=3
3320 X2=MX: Y2=MY
3321 IF MD=1 THEN X2=X2+1
3322 IF MD=2 THEN Y2=Y2-1
3323 IF MD=3 THEN X2=X2-1
3324 IF MD=4 THEN Y2=Y2+1
3330 IF X2<1 OR X2>37 OR Y2<1 OR Y2>22 THEN RETURN
3340 IF FN SC(0)<>42 THEN GOTO 3345
3341 PT=PT+1: IF PT=48 THEN RETURN
3342 T=4+INT(PT/12): IF T<>NM THEN NM=T: MU(NM,1)=X2: MU(NM,2)=MY
3345 SYS PLT,MU(1,1),MU(1,2): PRINT " ";: SYS PLT,X2,Y2: PRINT "{green}+"; 
3346 FOR I=1 TO NM-1: MU(I,1)=MU(I+1,1): MU(I,2)=MU(I+1,2): NEXT
3350 MU(NM,1)=X2: MU(NM,2)=Y2: MX=X2: MY=Y2
3355 RETURN
3400 FOR K=1 TO 2: MM=MM+1: IF MM>3 THEN MM=1
3401 I=MM: TX=ME(I,1): TY=ME(I,2)
3402 FL=((TX+3)/4)=INT((TX+3)/4) AND ((TY+2)/3)=INT((TY+2)/3)
3405 IF NOT FL THEN GOTO 3430
3407 ON1+8*RND(O) GOTO 3410, 3410, 3410, 3410, 3410, 3420, 3420, 3430
3410 IF RND(0)>.5 THEN GOTO 3413
3411 IF MX<TX THEN DE(I)=3: GOTO 3430
3412 IF MX>TX THEN DE(I)=1: GOTO 3430
3413 IF MY<TY THEN DE(I)=2: GOTO 3430
3414 IF MY>TY THEN DE(I)=4: GOTO 3430
3420 DE(I)=INT(4*RND(0))+1
3430 X2=TX: Y2=TY: TD=DE(I)
3435 IF TD=1 THEN X2=X2+1
3436 IF TD=2 THEN Y2=Y2-1
3437 IF TD=3 THEN X2=X2-1
3438 IF TD=4 THEN Y2=Y2+1
3440 IF X2<1 OR X2>37 OR Y2<1 OR Y2>22 THEN GOTO 3490
3445 SYS PLT,TX,TY: PRINT SP$(I);
3450 SP$(I)=CO$(FN CO(0)): T=FN SC(0): IF T=87 THEN T=151
3451 IF T=90 THEN T=122
3452 SP$(I)=SP$(I)+CHR$(T)
3455 SYS PLT,X2,Y2: PRINT "{white}o";: ME(I,1)=X2: ME(I,2)=Y2
3490 FOR I=1 TO 3: TX=ME(I,1): TY=ME(I,2)
3491 FOR J=1 TO NM
3492 IF TX=MU(J,1) AND TY=MU(J,2) THEN HI=1: GOTO 3495
3493 NEXT
3494 NEXT
3495 NEXT: RETURN
4000 :
4001 REM *** END
4002 :
4010 PRINT "{clear}": PRINT "THE GAME IS OVER!"
4015 IF PT=48 THEN PRINT "YOU GOT ALL THE POINTS (YOU WIN)": RETURN
4020 PRINT "YOU SCORED"PT"POINTS... GOOD EFFORT"
4990 RETURN