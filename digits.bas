10 REM ********************
11 REM ***              ***
12 REM ***    DIGITS    ***
13 REM ***              ***
14 REM ********************
15 REM
16 REM
20 GOSUB 1000
30 GOSUB 2000
40 GOSUB 3000
50 GOSUB 4000
60 END
1000 :
1010 REM *** INSTRUCTIONS
1020 :
1030 VT$="{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}"
1040 POKE 53280,6: POKE 53281,6
1050 GOSUB 1900
1060 PRINT "I WILL THINK OF A NUMBER BETWEEN 012"
1070 PRINT "AND 987. EACH DIGIT IN THE NUMBER WILL"
1080 PRINT "BE DIFFERENT FROM THE OTHER TWO.": PRINT
1090 PRINT "THE OBJECT OF THE GAME IS TO GUESS THE"
1100 PRINT "SOLUTION IN AS FEW TRIES AS POSSIBLE."
1110 GOSUB 1800: GOSUB 1900
1120 PRINT "AFTER EACH GUESS I WILL PRINT A HINT"
1130 PRINT "LINE AS FOLLOWS:": PRINT
1140 PRINT ">FOR EACH CORRECT DIGIT IN THE CORRECT"
1150 PRINT " POSITION, I WILL PRINT AN 'X'.": PRINT
1160 PRINT ">FOR A CORRECT DIGIT IN THE INCORRECT"
1170 PRINT " POSITION, I WILL PRINT AN 'O'.": PRINT
1180 PRINT ">FOR TOTALLY INCORRECT DIGITS, I WILL"
1190 PRINT " PRINT A '-'.": PRINT
1200 PRINT "PLAY WILL CONTINUE UNTIL YOU GUESS THE"
1210 PRINT "NUMBER. TO QUIT EARLY, SIMPLY HIT THE"
1220 PRINT "{reverse on}RETURN{reverse off} KEY FOR YOUR GUESS."
1230 GOSUB 1800: GOSUB 1900
1240 PRINT "HERE IS A TABLE TO HELP YOU UNDERSTAND"
1250 PRINT "HOW THE HINTS WORKS.": PRINT
1260 PRINT "ANSWER     GUESS    HINT LINE "
1270 PRINT "----------------------------------": PRINT
1280 PRINT " 065        703      O--": PRINT
1290 PRINT " 562        463      X--": PRINT
1300 PRINT " 918        890      OO-": PRINT
1310 PRINT " 390        305      XO-": PRINT
1320 PRINT " 271        721      XOO": PRINT
1330 PRINT " 425        780      ---": PRINT
1800 PRINT LEFT$(vt$,24);"PRESS {reverse on}RETURN{reverse off} WHEN READY TO CONTINUE:{reverse on} {reverse off}"
1810 GET AN$: IF AN$="" THEN GOTO 1810
1890 RETURN
1900 PRINT "{clear}{white}{down}{down}";TAB(13);"*** DIGITS ***": PRINT: PRINT
1990 RETURN
2000 :
2010 REM *** SETUP ***
2020 :
2030 PRINT "{clear}";
2040 DEF FNR(X)=INT(RND(1)*X)
2050 N1=FNR(10)
2060 N2=FNR(10): IF N1=N2 THEN GOTO 2060
2070 N3=FNR(10): IF N3=N1 OR N3=N2 THEN GOTO 2070
2990 RETURN
3000 :
3010 REM *** PLAY ***
3020 :
3030 GOSUB 1900
3040 PRINT "OKAY, I'VE GOT A NUMBER...": PRINT
3050 AN$="": INPUT "WHAT IS YOUR GUESS"; AN$: IF AN$="" THEN GOTO 3990
3060 IF LEN(AN$)=3 THEN GOTO 3080
3070 PRINT "TYPE THREE DIGITS PLEASE": GOTO 3050
3080 AN=VAL(AN$)
3090 IF AN<0 OR AN>999 OR AN<>INT(AN) THEN GOTO 3070
3100 G1=INT(AN/100)
3110 G2=INT((AN-G1*100)/10)
3120 G3=AN-(G1*100+G2*10)
3130 IF G1<>G2 AND G1<>G3 AND G2<>G3 THEN GOTO 3200
3140 PRINT "TYPE THREE DIFFERENT DIGITS PLEASE": GOTO 3050
3200 CP=0: CD=0: MI=0
3210 IF G1=N1 THEN CP=CP+1
3220 IF G2=N2 THEN CP=CP+1
3230 IF G3=N3 THEN CP=CP+1
3240 IF G1=N2 OR G1=N3 THEN CD=CD+1
3250 IF G2=N1 OR G2=N3 THEN CD=CD+1
3260 IF G3=N1 OR G3=N2 THEN CD=CD+1
3300 MI=3-CP-CD
3310 PRINT "FOR YOUR GUESS OF ";AN$;", I HINT ";
3320 IF CP>0 THEN FOR I=1 TO CP: PRINT "X";: NEXT
3330 IF CD>0 THEN FOR I=1 TO CD: PRINT "O";: NEXT
3340 IF MI>0 THEN FOR I=1 TO MI: PRINT "-";: NEXT
3350 PRINT: PRINT: NG=NG+1: IF CP<3 THEN GOTO 3050
3990 RETURN
4000 :
4010 REM *** END ***
4020 :
4030 PRINT "THE GAME IS OVER...": PRINT
4040 IF CP=3 THEN PRINT "YOU GUESSED IT IN ONLY";NG;"TRIES!"
4050 T$=""
4060 N$=STR$(N1)+STR$(N2)+STR$(N3)
4070 FOR I=1 TO LEN(N$): M$=MID$(N$,I,1): IF M$<>" " THEN T$=T$+M$
4080 NEXT
4090 IF CP<3 THEN PRINT "THE CORRECT NUMBER WAS ";T$
4100 PRINT "DO YOU WANT TO PLAY AGAIN? {reverse on} {reverse off}";
4110 GET AN$: IF AN$="" THEN GOTO 4110
4120 IF AN$="Y" THEN RUN
4130 PRINT "{left}N"
4990 RETURN