10 REM ********************
11 REM ***              ***
12 REM ***   REVERSER   ***
13 REM ***              ***
14 REM ********************
15 REM
16 REM
20 GOSUB 1000
30 GOSUB 2000
40 GOSUB 3000
50 GOSUB 4000
60 END
1000 :
1010 REM *** INSTRUCTIONS ***
1020 :
1030 POKE 53280,6: POKE 53281,6
1040 VT$="{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}"
1050 GOSUB 1900
1060 PRINT "IN THIS GAME YOU ARE GIVEN A LIST OF"
1070 PRINT "NUMBERS FROM 0 TO 9. THE LIST WILL NOT"
1080 PRINT "BE IN SEQUENCE. IT IS YOUR JOB TO SORT"
1090 PRINT "THE LIST INTO ASCENDING ORDER.": PRINT
1100 PRINT "YOU ARRANGE THE LIST BY REVERSING THE"
1110 PRINT "ORDER OF IT. YOU INPUT THE STARTING"
1120 PRINT "COLUMN THAT IS TO BE REVERSED, AND THAT"
1130 PRINT "COLUMN ALL THE WAY THROUGH TO COLUMN 9"
1140 PRINT "WILL BE REVERSED."
1150 GOSUB 1800: GOSUB 1900
1160 PRINT "IF YOU HAD THIS LIST:": PRINT
1170 PRINT "POSITIONS: 0 1 2 3 4 5 6 7 8 9"
1180 PRINT "           -------------------"
1190 PRINT "     LIST: 0 1 9 8 7 6 5 2 3 4": PRINT
1200 PRINT "AND YOU REVESRED IT POSITION 7, IT WOULD";
1210 PRINT "LOOK LIKE THIS.": PRINT
1220 PRINT "POSITIONS: 0 1 2 3 4 5 6 7 8 9"
1230 PRINT "           -------------------"
1240 PRINT "     LIST: 0 1 9 8 7 6 5 4 3 2": PRINT
1250 GOSUB 1800: GOSUB 1900
1260 PRINT "A FINAL REVERSAL AT POSITION 2 WOULD"
1270 PRINT "COMPLETE THE LIST AS THIS:": PRINT
1280 PRINT "POSITIONS: 0 1 2 3 4 5 6 7 8 9"
1290 PRINT "           -------------------"
1300 PRINT "     LIST: 0 1 2 3 4 5 6 7 8 9": PRINT
1310 PRINT "YOU WIN WHEN THE LIST IS SORTED IN"
1320 PRINT "ASCENDING ORDER AS IS SHOWN ABOVE.": PRINT
1330 PRINT "GOOD LUCK!"
1800 PRINT LEFT$(VT$,24);"PRESS {reverse on}RETURN{reverse off} WHEN READY TO CONTINUE:{reverse on} "
1810 GET AN$: IF AN$="" THEN GOTO 1810
1890 RETURN
1900 PRINT "{clear}{white}{down}{down}";TAB(12);"*** REVERSER ***": PRINT: PRINT
1990 RETURN
2000 :
2010 REM *** SETUP ***
2020 :
2030 DIM LI(9)
2040 FOR I=0 TO 9: LI(I)=I: NEXT
2050 FOR I=0 TO 9:X=INT(RND(1)*10): T=LI(I): LI(I)=LI(X): LI(X)=T: NEXT
2990 RETURN
3000 :
3001 REM *** PLAY
3002 :
3010 GOSUB 1900
3020 PRINT "POSITIONS: 0 1 2 3 4 5 6 7 8 9"
3021 PRINT "           -------------------"
3022 PRINT "     LIST:";
3030 FOR I=0 TO 9: PRINT LI(I);: NEXT: PRINT
3035 FOR I=0 TO 9: IF LI(I)=I THEN NEXT: RETURN
3040 PRINT: INPUT "REVERSE AT WHICH POISTION (0-9)";AN$: AN=VAL(AN$)
3041 IF AN$<"0" OR AN$>"9" THEN PRINT "TYPE A NUMBER BETWEEN 0 AND 9": GOTO 3040
3050 MDL=INT((9-ANS)/2): FOR I=AN TO AN+MDL: T=LI(I): LI(I)=LI (9+AN-I): LI(9+AN-I)=T
3051 NEXT
3060 MOVE=MOVE+1 : GOTO 3010
4000 :
4001 REM *** END
4002 :
4010 PRINT: PRINT "YOU DID IT!": PRINT: PRINT "IT TOOK YOU A TOTAL OF "MO"MOVES."
4020 PRINT
4030 IF MO<11 THEN PRINT "THATS SUPER!": RETURN
4031 IF MO<14 THEN PRINT "GOOD JOB!": RETURN
4032 IF MO<17 THEN PRINT "THAT WAS JUST SO-SO": RETURN
4034 IF MO>19 THEN PRINT "BACK TO SCHOOL!": RETURN
4040 PRINT "GOOD JOB1"
4990 RETURN