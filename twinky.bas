10 REM ********************
11 REM ***              ***
12 REM ***    TWINKY    ***
13 REM ***              ***
14 REM ********************
15 REM
16 REM
20 GOSUB 1000
30 GOSUB 2000
40 GOSUB 3000
90 END
1000 :
1010 REM *** INSTRUCTIONS
1020 :
1030 POKE 53280,6: POKE 53281,6
1040 VT$="{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}"
1050 GOSUB 1900
1060 PRINT "THIS IS THE GAME OF TWINKY. IN IT YOU"
1070 PRINT "PRETEND TO BE A SPACE EXPLORER WHO HAS"
1080 PRINT "LANDED ON A HOSTILE PLANET.": PRINT
1090 PRINT "CAPTURED BY THE UNFRIENDLY NATIVES, YOU"
1100 PRINT "ARE TOSSED INTO A LARGE PRISON ALONG"
1110 PRINT "WITH A FEROCIOUS TWINKY.": PRINT
1120 PRINT "A TWINKY IS A HORRIBLE CREATURE THAT"
1130 PRINT "WILL CATCH YOU AND ABSORB YOUR BODY INTO";
1140 PRINT "HIS IF HE GETS CLOSER THAN TWO UNITS"
1150 PRINT "AWAY FROM YOU."
1160 GOSUB 1800: GOSUB 1900
1170 PRINT "IN THE INTEREST OF FAIR PLAY, YOU ARE"
1180 PRINT "GIVEN A ZAP GUN THAT WILL TEMPORARILY"
1190 PRINT "CHASE THE TWINKY AWAY.": PRINT
1200 PRINT "ALSO, IF YOU CAN MAKE IT TO THE SPECIAL"
1210 PRINT "OBJECTIVE SQUARE BEFORE BEING ABSORBED,"
1220 PRINT "YOU WILL BE SET FREE.": PRINT
1230 PRINT "AFTER YOU MOVE, YOU WILL BE INFORMED OF"
1240 PRINT "YOUR DISTANCE FROM THE OBJECTIVE SQUARE"
1250 PRINT "AS WELL AS FROM THE TWINKY."
1260 GOSUB 1800: GOSUB 1900
1270 PRINT "THERE ARE SEVERAL OTHER OBJECTS WITHIN"
1280 PRINT "THE MAZE WHICH ARE OF INTEREST.": PRINT
1290 PRINT "THERE ARE TWENTY RELOCATION SQUARES."
1300 PRINT "THESE SQUARES SEND YOU TO SOME OTHER"
1310 PRINT "SECTION OF THE MAZE.": PRINT
1320 PRINT "THERE ARE TWENTY IMPREGNALBE SQUARES"
1330 PRINT "WHICH YOU CANNOT ENTER.": PRINT
1340 PRINT "THERE IS ONE SUPER DEADLY KILLER SQUARE"
1350 PRINT "WHICH ENDS YOUR ORDEAL QUICKLY AND"
1360 PRINT "PAINLESSLY.": PRINT
1370 PRINT "THAT'S IT... TRY TO ENJOY IT!"
1800 PRINT LEFT$(VT$,24);"PRESS {reverse on}RETURN{reverse off} WHEN READY TO CONTINUE:{reverse on} "
1810 GET AN$: IF AN$="" THEN GOTO 1810
1890 RETURN
1900 PRINT "{clear}{white}{down}{down}";TAB(13);"*** TWINKY ***": PRINT: PRINT
1990 RETURN
2000 :
2010 REM *** SETUP ***
2020 :
2030 GOSUB 1900
2040 DIM MA(15,15)
2050 DEF FNR(X)=INT(RND(1)*X)+1
2060 DEF FNA(X)=.001*INT(X*1000+.5)
2090 :
2100 REM *** BLOCKED
2110 FOR I=1 TO 20
2120 X=FNR(15): Y=FNR(15)
2130 IF MA(X,Y) THEN GOTO 2120
2140 MA(X,Y)=1
2150 NEXT
2190 :
2200 REM *** RELOCATION
2210 FOR I=1 TO 20
2220 X=FNR(15): Y=FNR(15)
2230 IF MA(X,Y) THEN GOTO 2220
2240 MA(X,Y)=2
2250 NEXT
2290 :
2300 REM *** SUPER KILL
2320 X=FNR(15): Y=FNR(15)
2330 IF MA(X,Y) THEN GOTO 2320
2340 MA(X,Y)=3
2390 :
2400 REM *** OBJECTIVE
2410 XO=FNR(15): YO=FNR(15)
2420 IF MA(XO,YO) THEN GOTO 2420
2430 MA(XO,YO)=4
2490 :
2500 REM *** TWINKY
2520 XT=FNR(15): YT=FNR(15)
2530 IF MA(XT,YT) THEN GOTO 2520
2540 MA(XT,YT)=5
2590 :
2600 REM *** PLAYER
2620 XP=FNR(15): YP=FNR(15)
2630 IF MA(XP,YP) THEN GOTO 2620
2640 MA(XP,YP)=6
2690 :
2900 SP=0: SS=0
3000 :
3010 REM *** PLAY ***
3020 :
3030 GOSUB 3600
3040 GOSUB 3900: IF WL THEN GOTO 3990
3050 PRINT: SM$="": INPUT "MOVE OR SHOOT (M/S) ";SM$: SM$=LEFT$(SM$,1)
3060 IF SM$<>"M" AND SM$<>"S" THEN PRINT "ENTER 'M' OR 'S'": GOTO 3050
3100 PRINT: PRINT "FORWARD, BACKWARD,"
3110 INPUT "RIGHT OR LEFT (F/B/R/L) ";AN$: AN$=LEFT$(AN$,1)
3120 IF AN$="F" OR AN$="B" OR AN$="R" OR AN$="L" THEN GOTO 3140
3130 PRINT "ENTER 'F', 'B', 'R' OR 'L'": GOTO 3100
3140 IF AN$="F" THEN X=0:Y=-1
3150 IF AN$="B" THEN X=0:Y=1
3160 IF AN$="R" THEN X=1:Y=0
3170 IF AN$="L" THEN X=-1:Y=0
3180 IF SM$="S" THEN GOTO 3400
3200 X=X+XP: Y=Y+YP
3210 IF X>1 AND X<=15 AND Y>1 AND Y<=15 THEN GOTO 3220
3212 PRINT "MOVE NOT ALLOWED"
3214 PRINT "THAT WOULD TAKE YOU OUT OF THE MAZE": GOTO 3500
3220 IF MA(X,Y)<>1 THEN GOTO 3230
3222 PRINT "MOVE NOT ALLOWED"
3224 PRINT "THAT SPACE IS BLOCKED"
3226 GOTO 3500
3230 IF MA(X,Y)<>2 THEN GOTO 3240
3232 PRINT "YOU'VE BEEN RELOCATED...."
3234 X=FNR(15): Y=FNR(15)
3236 IF MA(X,Y)=1 THEN GOTO 3234
3238 GOTO 3230
3240 IF MA(X,Y)<>3 THEN GOTO 3250
3242 PRINT "MOVE ALLOWED, BUT ..."
3244 PRINT "YOU FOUND THE SUPER KILL SQUARE!"
3246 PRINT "YOU'VE BEEN KILLED!": GOSUB 60300: WL=1: GOTO 3990
3250 IF MA(X,Y)<>4 THEN GOTO 3260
3252 PRINT "MOVE ALLOWED"
3254 PRINT "YOU FOUND THE OBJECTIVE!"
3256 PRINT "YOU WIN A TRIP OFF THIS PLANET!": WL=0: GOTO 3990
3260 IF MA(X,Y)<>5 THEN GOTO 3270
3262 PRINT "MOVE ALLOWED": PRINT
3264 PRINT "<<< SCHLOORP! >>>": GOSUB 60300: WL=1: GOTO 3990
3270 PRINT "MOVE ALLOWED"
3280 MA(XP,YP)=SP: XP=X: YP=Y: SP=MA(XP,YP): MA(XP,YP)=6: GOTO 3500
3400 SX=XP: SY=YP
3410 SX=SX+X: SY=SY+Y: PRINT "ZAP--";
3420 IF SX>=1 AND SX<=15 AND SY>=1 AND SY<=15 THEN GOTO 3430
3422 PRINT "FIZZLE...."
3424 PRINT "THE SHOT LEFT THE MAZE."
3426 PRINT "THE SHOT MISSED THE TIWNKY!": GOTO 3500
3430 IF MA(SX,SY)=0 OR MA(SX,SY)=2 OR MA(SX,SY)=3 OR MA(SX,SY)=4 THEN GOTO 3410
3440 IF MA(SX,SY)<>1 THEN GOTO 3452
3442 PRINT "BLAST!"
3444 PRINT "THE SHOT HIT A WALL!"
3446 PRINT "THE SHOT MISED": GOTO 3500
3452 PRINT "OUCH!"
3454 PRINT "THE SHOT HIT THE TWINKY"
3456 PRINT "THE TWINKY RETREATS"
3460 MA(SX,SY)=SS: XT=FNR(15): YT=FNR(15): SS=MA(XY,YT): MA(XT,YT)=5
3500 REM *** TWINKY MOVE LOGIC
3510 GOSUB 3600
3520 PRINT: PRINT "THE TWINKY MOVES..."
3530 FOR I=1 TO 500: NEXT
3540 GOSUB 3900: IF WL THEN GOTO 3990
3550 IF XP<XT THEN X=-1: Y=0: GOTO 3590
3560 IF XP>XT THEN X=1: Y=0: GOTO 3590
3570 IF YP<YT THEN X=0: Y=-1: GOTO 3590
3580 IF YP>YT THEN X=0: Y=1: GOTO 3590
3590 MA(XT,YT)=SS: XT=XT+X: YYT+Y: SS=MA(XT,YT): MA(XT,YT)=5: GOTO 3000
3600 :
3610 DT=FNA(SQR((XT-XP)^2+(YT-YP)^2))
3620 DO=FNA(SQR((XP-XO)^2+(YP-YO)^2))
3630 PRINT: PRINT "THE TWINKY IS";DT;"UNITS AWAY."
3640 PRINT: PRINT "THE OBJECTIVE IS";DO;"UNITS AWAY."
3690 RETURN
3900 IF DT>=2 THEN GOTO 3990
3910 PRINT: PRINT "<<< SCHLOORP! >>>"
3920 PRINT "YOU'VE BEEN ABSORBED BY THE TWINKY!"
3930 PRINT "YOU LOSE.": GOSUB 60300: WL=1
3990 RETURN
60300 :
60310 REM BELL SOUND ROUTINE
60320 :
60330 SD=54272: POKE SD+24,15
60340 POKE SD+5,7: POKE SD+6,0
60350 POKE SD+1,90: POKE SD+4,33
60360 FOR TS=1 TO 100: NEXT
60370 POKE SD+4,0: POKE SD+1,0
60390 RETURN